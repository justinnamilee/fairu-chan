#!/usr/bin/perl

  #######################
  # see -h or POD below #
  #######################


use strict;

use File::Basename qw(basename dirname);
use File::Copy qw(copy move);
use Getopt::Long;


# constants
sub PATH_SEP() { q[/] } # change me if needed (shouldn't need to)
sub FILT_SEP() { q[,] }
sub OUT_SEP()  { q[, ] }
sub OUT_LIST() { qw(old new state) }


# sub defs
sub pathify(_;@);
sub scan(_);
sub help();


# global variables in order of OPTIONS + defaults
my $name = basename($0);
my $help = 0;
my $verbose = 0;
my $copy = 0; # default is to move
my $directory = '.';
my $run = 0;
my $recursive = 0;
my $filter = qr//; # where the final filter result goes
my $match = undef;
my %map = ();
my $output = undef;
my $wait = 0;
my $internal = 0; # use rename instead of File::Copy::move()
my $manual = 0; # start in automatic mode


# check for better help
BEGIN:
{
  eval {
    require Pod::Help;
  };

  warn qq[Consider installing Pod::Help for better help.\n] if ($@);
}


# option import
GETOPT:
{
  Getopt::Long::Configure (q[bundling], q[ignorecase_always]);

  my ($filter_in, $subs_in) = ();

  my %input = (
    q[help|h]         => \$help,
    q[verbose|v+]     => \$verbose,
    q[copy|c]         => \$copy,
    q[directory|d=s]  => \$directory,
    q[execute|e]      => \$run,
    q[recursive|r]    => \$recursive,
    q[filter|f=s]     => \$filter_in,
    q[subroutine|s=s] => \$subs_in,
    q[wait|w]         => \$wait,
    q[internal|i]     => \$internal,
    q[manual|m]       => \$manual,
  );

  # import options
  die qq[Invalid option, see help ($name -h)] unless (GetOptions(%input));

  # help?
  help if ($help or @ARGV < 2);

  # where, yo?
  die qq[Bad directory: $!] unless (-d $directory);

  # enable extension filtering
  my @filter = ();

  if (length($filter_in))
  {
    push(@filter, split(FILT_SEP, $filter_in));
  }

  if (@filter)
  {
    my $qr = q[\\.(?:] . join(q[|], @filter) . q[)$]; # jank

    $filter = qr/$qr/i;
  }

  # generate new maps
  %map = (defined($subs_in) ? eval $subs_in : ());

  die qq[Learn to write subroutines: $@] if $@;

  # matching regex
  die q[Zero length matching regex] unless (length($ARGV[0]));

  eval {
    $match = qr/$ARGV[0]/;
  };

  die qq[Unable to compile matching regex: $@] if ($@);

  # output format
  die q[Zero length output format] unless (length($output = $ARGV[1]));
}


# print extra-extra information if requested
if ($verbose > 1)
{
  print STDERR qq[[DEBUG] Mode:      ]. ($copy ? q[Copy] : q[Move]) .qq[\n];
  print STDERR qq[[DEBUG] Filter:    $filter\n];
  print STDERR qq[[DEBUG] Directory: $directory\n];
  print STDERR qq[[DEBUG] Regex:     $match\n];
  print STDERR qq[[DEBUG] Recursive: ]. ($recursive ? q[True] : q[False]) .qq[\n];
  print STDERR qq[[DEBUG] Format:    $output\n];
  print STDERR qq[[DEBUG] Maps:      ]. int(keys(%map)) .qq[\n];
  print STDERR qq[[DEBUG] Wait:      ]. ($wait ? q[True] : q[False]) .qq[\n];
  print STDERR qq[[DEBUG] Dry Run:   ]. ($run ? q[False] : q[True]) .qq[\n];
  print STDERR qq[[DEBUG] Internal:  ]. ($internal ? q[True] : q[False]) .qq[\n];
  print STDERR qq[[DEBUG] Manual:    ]. ($manual ? q[True] : q[False]) .qq[\n\n];
}


# pre-filter for certain file extensions
my @file = grep { $_ =~ $filter } scan($directory);
my %file = ();


# parse the extra information out automatically
unless ($manual)
{
  for (my $i = 0; $i < @file; $i++)
  {
    my ($dir, $f, $ext) = (dirname($file[$i]), basename($file[$i]), q[]);

    unless (length($dir))
    {
      $dir = q[.];
    }

    if ($f =~ s/^(.+)(\..+?)$/$1/)
    {
      $ext = $2;
    }

    $file{$f} = { dir => $dir, ext => $ext };
    $file[$i] = $f;
  }
}


# do it, yo, get to processin'
foreach my $old (@file)
{
  my $new;

  if ($old =~ $match)
  {
    # this maps the matched elements to modifiers (if present) then passes them to sprintf in sort order
    $new = sprintf($output, map { exists($map{$_}) ? $map{$_}->($+{$_}) : $+{$_} } sort keys(%+));

    # complete automatic mode
    unless ($manual)
    {
      $new = pathify($file{$old}->{dir}, $new) . $file{$old}->{ext};
      $old = pathify($file{$old}->{dir}, $old) . $file{$old}->{ext};
    }

    # do the deed, do it, sir
    if ($run && !$copy && ($internal ? rename($old, $new) : move($old, $new)))
    {
      print qq[Move: '$old' -> '$new'\n] if $verbose;
    }
    elsif ($run && $copy && copy($old, $new))
    {
      print qq[Copy: '$old' -> '$new'\n] if $verbose;
    }
    elsif ($run) # catch error
    {
      warn qq[Warn: $!\n];
    }
    else # dry run mode
    {
      print qq[Plan: '$old' -> '$new'\n];
    }
  }
  else # no match
  {
    print qq[Skip: '$old'\n] if $verbose;
  }
}


# wrap it up, buddy
if ($wait)
{
  print STDERR qq[\nPress <ENTER> to quit...\n];
  <STDIN>;
}

exit 0;



##
# subroutines


# make paths
sub pathify(_;@)
{
  join(PATH_SEP, @_)
}


# non/recursive file scanner
sub scan(_)
{
  my ($path, @file) = @_;

  if (-f $path)
  {
    push(@file, $path);
  }
  else
  {
    if (-d $path && opendir(my $d, $path))
    {
      if ($recursive)
      {
        push(@file, map { scan } map { pathify($path, $_) } grep { !/^\.\.?$/ } readdir($d));
      }
      else
      {
        push(@file, map { pathify($path, $_) } grep { !/^\.\.?$/ } readdir($d));
      }
    }
    else
    {
      warn qq[Warn: '$path': $!];
    }
  }

  return (@file);
}


# really? you need a comment?
sub help()
{
  eval {
    # send text mode (-t), no pager (-T), and file (-F $0)
    Pod::Help->help(qw[-t -T -F], $0);
  };

  if ($@)
  {
    warn qq[Consider installing Pod::Help for better help.\n];
    warn qq[\nUsage:\n];
    warn qq[\t\$ $name [-hvcerwim -d <path> -f <filter> -s <subs>] <1:regex> <2:format> \n];
    warn qq[\n\t\tRequired:\n];
    warn qq[\n\t\t <1:regex>  -> Regex match string, should use named capture groups, see POD for details.\n];
    warn qq[\n\t\t <2:format> -> Output string in Perl sprintf format, sorted capture groups as input, see POD for details.\n];
    warn qq[\n\t\tOptional:\n];
    warn qq[\n\t\t-h -> Display help message.\n];
    warn qq[\n\t\t-v -> Enable verbose output, add two for debug output.\n];
    warn qq[\n\t\t-c -> Enable copy mode (instead of move).\n];
    warn qq[\n\t\t-e -> Enable "execute" mode, otherwise this script just says what it would do without doing it.\n];
    warn qq[\n\t\t-r -> Enable folder recursion on file scan.\n];
    warn qq[\n\t\t-w -> Do not exit script after completion, what for <ENTER> key before hand.\n];
    warn qq[\n\t\t-i -> Use internal (Perl) rename function instead of File::Copy::move(), does nothing for copy mode.\n];
    warn qq[\n\t\t-m -> Extended matching mode, don't do any "smart" things to the path before matching against regex, see POD for details.\n];
    warn qq[\n\t\t-d -> Scan for files in <path> directory.\n];
    warn qq[\n\t\t-f -> Set filters manually from <filter> string, comma-separated suffixes (e.g. "mkv,mp4").\n];
    warn qq[\n\t\t-s -> Mapping subroutines for named capture groups before going to output, see POD for details.\n];
    warn qq[\nExample:\n];
    warn qq[\n\t\$ rename -d ~/Pictures/Move_Nums_to_Front '^(?<b_front>.+?) - (?<a_num>\d+) - (?<c_rear>.+?)$' '%03d. %s - %s' -e\n];
    warn qq[\n\nFor more information see POD.\n];
  }

  exit 1;
}



__END__


=pod

=head1 NAME

RENAME.PL - Pretty under-engineered if you ask me.

=head1 USAGE

RENAME.PL [-hvcerwim -d <path> -f <filter> -s <map>] <1:regex> <2:format>

=head1 DESCRIPTION

Rename files and stuff with the power of B<REGEX> and B<Perl>.  It uses named capture groups in B<REGEX>, optionally passes them to a matching named subroutine, then it stuffs them into your provided format for C<sprintf()>.

=head1 THE EASY STUFF

=over

=item HELP B<-h|--help>

Duh.

=item VERBOSE B<-v|--verbose>

Print's event information as it happens.  Add a second for B<DEBUG> mode.

=item COPY B<-c|--copy>

Do you want to copy the files instead of moving them (default)?

=item DIRECTORY B<-d|--directory path>

Defaults to '.', make sure you have your filter set!

=item RUN B<-e|--execute>

If this is not include the script does nothing to the actual files (default).

=item RECURSIVE B<-r|--recursive>

Do I scan sub-directories as well?

=item FILTERS B<-f|--filter list>

Only grab paths that match these pre-filters, compiled as C<qr/@{[ q[\\.(?:] . join(q[|], @filter) . q[)$] ]}/i>.

Input should be a string of extensions separated by FILT_SEP (defaults to ','), like: 'mkv,mp4'.

=item WAIT B<-w|--wait>

Do not exit the script until enter is pressed.

=item INTERNAL B<-i|--internal>

Use internal C<rename()> instead of C<File::Copy::move()>.  This option is probably not what you want ever.

=item MANUAL B<-m|--manual>

Disable automatic "path" and "extension" capture.  You will need to provide matches for your path that come first and extension that come last (or not, whatever).

=back

=head1 GORY DETAILS

=head2 INPUT REGEX B<ARG1>

Your B<REGEX> will be passed the complete file name from the input path (B<-d> or '.' by default).  It must return a successful match or the file in question will be skipped.

To get data to the output formatter capture groups are used.  The capture groups MUST be named and will be sorted by alphabetical order before being passed to the output formatter.

=head3 Example (with B<-m>):

  INPUT: '~/videos/bad_named_tv/your_show_310_h264_1080p.mkv'
  REGEX: qr/^(?<a_path>.+?)your_show(?<b_season>\d)(?<c_episode>\d+).+$/

=head3 Results:

  {
    a_path    => '~/videos/bad_named_tv/',
    b_season  => 3,
    c_episode => 10
  }

=head2 OUTPUT FORMAT B<ARG2>

Once all named capture groups have been mapped (if applicable), they are sorted alphabetically and stuffed into C<sprintf()>.

=head2 MAPPING SUBROUTINES B<-s|--subroutine hash>

After matching has been completed on an input path B<rename.pl> will look for subroutines to be called on each named capture group's result.  Input to B<-s> should be a string containing a list of key-value pairs, like:

  -s 'a_one => sub {uc(shift)}, b_two => sub {lc(shift)}'

=head3 Example (with B<-m>):

  INPUT: { a_path => '~/videos/bad_named_tv/' }
  MAP:   { a_path => sub { my (@path) = split('/',shift); pop(@path); push(@path, 'sorted_tv'); return join('/',@path) } }

=head3 Results:

  {
    a_path    => '~/videos/sorted_tv',
    b_season  => 3,
    c_episode => 10
  }

=head3 Example (with B<-m>):

  INPUT:  { a_path => '~/videos/sorted_tv', b_season => 3, c_episode => 10 }
  FORMAT: '%s/Your Show S%02dE%02d.mkv'

=head3 Results:

  '~/videos/sorted_tv/Your Show S03E10.mkv'

=head1 EXAMPLES

  Convert TV named like "~/videos/bad_named_tv/SomeShow - 6.66.mp4" to "~/videos/bad_named_tv/SomeShow - S06E66.mp4"

    $ rename.pl -m -d ~/videos/bad_named_tv '^(?<a_path>.+?)s+-s+(?<b_sn>\d).(?<c_en>\d\d).(?<d_ext>.+)' '%s - S%02dE%02d.%s' -e

  Convert by copying a 6-disc album (20 tracks per disc) named like "~/music/bad_multi_cd/Artist [Album 6CD Set] Disk 3.16.mp3" to "~/music/bad_multi_cd/Artist (Album) 056.mp3"

    $ rename.pl -cd ~/music/bad_multi_cd '^(?<a_artist>.+?)\s+\[(?<b_album>.+?)\]\s+Disk\s+(?<c_num>\d\.\d+)' '%s (%s) %03d' -s 'c_num => sub { my ($d,$t) = split(q[.], shift); return (($d-1)*20 + $t) }' 

  Take files out of subfolders and put them into the main directory by converting 'Season 1/Johnny Bravo 1x03b Some title.avi' to 'Johnny Bravo S01E03 (Part 02) Some title.avi', etc.

    $ rename.pl '^.+? - (?<a_season>\d+)x(?<b_episode>\d+)(?<c_part>\w) - (?<d_extra>.+)' 'Johnny Bravo S%02dE%02d (Part %02d) %s' -s 'c_part => sub { return (defined($_[0]) ? (ord(shift) - 96) : 0) }' -rm

=head1 AUTHOR

Justin Lee L<kool dot name at gmail dot com>

=head1 HOMEWORK

=over

=item L<perlre>: https://perldoc.perl.org/perlre.html

=item L<perlsub>: https://perldoc.perl.org/perlsub.html

=back

=cut
